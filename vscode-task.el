(defun vscode-task--find-tasks (root)
  (let ((tasks (concat root ".vscode/tasks.json")))
    (if (file-exists-p tasks)
        (let* ((json-object-type 'hash-table)
               (json-array-type 'list)
               (json-key-type 'string)
               (json (json-read-file tasks)))
          json)
      (message "Unable to locate task definitions"))))

(defun vscode-task--select-task (task-defns)
  "Prompt the user to select a task to run"
  (let* ((tasks  (gethash "tasks" task-defns))
         (labels (mapcar (lambda (task) (gethash "label" task))
                         tasks))
         (selected (completing-read "Run Task: " labels)))
    (car (seq-filter (lambda (task) (string= (gethash "label" task) selected))
                     tasks))))

(defun vscode-task--execute-task (root task-defn)
  (let ((type (gethash "type" task-defn)))
    (cond ((string= "shell" type)
           (let ((default-directory root)
                 (command (gethash "command" task-defn)))
             (compile command)))
          (t (message "Only 'shell' tasks are currently supported")))))

(defun vscode-task-run ()
  "Choose and run a task definition."
  (interactive)
  (let* ((root (car (project-roots (project-current t))))
         (task-defns (vscode-task--find-tasks root))
         (selected (vscode-task--select-task task-defns)))
    (vscode-task--execute-task root selected)))
